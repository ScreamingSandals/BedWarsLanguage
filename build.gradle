plugins {
    id 'org.screamingsandals.plugin-builder-lite' version "${SCREAMING_PLUGIN_BUILDER}" apply false
}

defaultTasks 'clean', 'screamCompile'

group = 'org.screamingsandals.language.bedwars'
version = TRANSLATION_BRANCH + '-SNAPSHOT-local'

apply plugin: 'org.screamingsandals.plugin-builder-lite' // we need to apply the plugin later

sourceSets {
    main {
        output.dir(new File(projectDir, "build/generated/buildinfo"), builtBy: 'generateBuildInfoFile')
        resources {
            srcDirs "."
            include '/languages/**'
        }
    }
}

task generateBuildInfoFile {
    def outputDir = new File(projectDir, "build/generated/buildinfo")
    doFirst {
        mkdir outputDir


        new File(outputDir, "language_definition.json").withWriter { writer ->
            def build
            if (System.getenv('BUILD_NUMBER') != null) {
                build = System.getenv('BUILD_NUMBER')
            } else {
                build = "custom"
            }
            def contents = "{\"branch\": \"${TRANSLATION_BRANCH}\", \"version\": \"${build}\", \"languages\":{"

            def first = true
            new File(projectDir, "languages").listFiles().each {
                if (!first) {
                    contents += ","
                } else {
                    first = false
                }
                def locale = (it.name =~ /[a-z]{2}-[A-Z][A-Za-z]{1,3}/).findAll()?.first()
                contents += "\"${locale}\": \"languages/${it.name}\""
            }
            contents += "}}"
            writer.write(contents)
        }
    }
}

sourceCompatibility = '11.0'